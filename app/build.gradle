apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

android {
    def config = rootProject.ext
    compileSdkVersion config.androidCompileSdkVersion
    buildToolsVersion config.androidBuildToolsVersion
    defaultConfig {
        applicationId "com.malec.cheesetime"
        minSdkVersion config.androidMinSdkVersion
        targetSdkVersion config.androidTargetSdkVersion
        versionCode config.appVersionCode
        versionName config.appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            File signFile = rootProject.file('release_keystore.properties')
            Properties props = new Properties()
            props.load(new FileInputStream(signFile))
            storeFile file(props['keystore'])
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    def dependencies = rootProject.ext.dependencies
    def daggerDependencies = dependencies.dagger
    def firebaseDependencies = dependencies.firebase
    def androidDependencies = dependencies.android
    def testDependencies = dependencies.test

    //dagger
    kapt                      daggerDependencies.androidProcessor
    kapt                      daggerDependencies.compiler
    implementation            daggerDependencies.android
    implementation            daggerDependencies.androidSupport

    //firebase
    implementation            firebaseDependencies.auth
    implementation            firebaseDependencies.storage
    implementation            firebaseDependencies.playServicesAuth
    implementation            firebaseDependencies.database
    implementation            firebaseDependencies.uiStorage
    implementation            firebaseDependencies.firestoreKtx
    implementation            firebaseDependencies.kotlinxCoroutinesPlayServices

    //android
    implementation            androidDependencies.appcompat
    implementation            androidDependencies.coreKtx
    implementation            androidDependencies.constraintlayout
    implementation            androidDependencies.legacySupportV4
    implementation            androidDependencies.recyclerview
    implementation            androidDependencies.vectordrawable
    implementation            androidDependencies.material

    //glide
    kapt                      dependencies.glide.compiler
    implementation            dependencies.glide.core

    //navigation
    implementation            dependencies.navigation.ui
    implementation            dependencies.navigation.fragment

    //zxing
    implementation            dependencies.zxing.core
    implementation            dependencies.zxing.android

    def lifecycle_version = "2.3.1"
    kapt                      "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation            "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation            "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    kapt                      dependencies.databinding

    implementation            dependencies.cicerone

    implementation            dependencies.gson

    implementation            dependencies.loopinglayout

    implementation            dependencies.photoView

    //kotlin
    implementation            dependencies.kotlinStdlib
    implementation            dependencies.coroutines

    implementation            project(':core:store')
    implementation            project(':core:presentation')

    //test
    testImplementation        testDependencies.junit
    testImplementation        testDependencies.coroutines
    testImplementation        testDependencies.mockk
    androidTestImplementation testDependencies.androidMockk
    androidTestImplementation testDependencies.androidJunit
    androidTestImplementation testDependencies.androidEspresso
}
